plugins {
    id 'java'
    id 'checkstyle'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'

// -- jenkins -- //
    id 'jacoco'
    id 'org.sonarqube' version '4.4.1.3373'
}

sonar {
    properties {
        property 'sonar.projectKey', 'courm-proejct'
        property 'sonar.projectName', 'courm-proejct'
        property 'sonar.host.url', 'http://sonarqube:9000'
        property 'sonar.coverage.jacoco.xmlReportPaths',
                'build/reports/jacoco/test/jacocoTestReport.xml'
    }
}

jacoco {
    toolVersion = '0.8.11'
}

jacocoTestReport {
    classDirectories.setFrom(files(classDirectories.files.collect { dir ->
        fileTree(dir: dir, exclude: [
                "**/Q*",
                '**/*$*',
                "**/generated/**"
        ])
    }))
    reports {
        xml.required = true  // SonarQube용
        html.required = true
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
// -- jenkins -- //

group = 'com.groom'
version = '0.0.1-SNAPSHOT'
description = 'group project for Spring Boot'

// naver code convention
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

checkstyle {
    maxWarnings = 0 // 규칙이 어긋나는 코드가 하나라도 있을 경우 빌드 fail을 내고 싶다면 이 선언을 추가한다.
    configFile = file("${rootDir}/naver-checkstyle-rules.xml")
    configProperties = ["suppressionFile": "${rootDir}/naver-checkstyle-suppressions.xml"]
    toolVersion = "10.12.3" // checkstyle 버전 8.24 이상 선언
}
// --------------

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.retry:spring-retry"

    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // Testcontainers
    testImplementation 'org.testcontainers:junit-jupiter:1.19.7'
    testImplementation 'org.testcontainers:postgresql:1.19.7'

    testImplementation 'io.rest-assured:rest-assured'

    // OpenFegin
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2025.0.1"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
