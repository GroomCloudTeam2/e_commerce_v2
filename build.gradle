plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'org.sonarqube' version '5.0.0.4638'
    id 'org.springframework.boot' version '3.5.9' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.groom'
version = '0.0.1-SNAPSHOT'
description = 'group project for Spring Boot'

ext {
    springCloudVersion = "2025.0.0"
}

allprojects {
    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        // ===== Spring Boot 기본 =====
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter'


        // ===== OpenFeign / Cloud =====
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
        implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'

        // ===== Retry =====
        implementation 'org.springframework.retry:spring-retry'

        // ===== JWT =====
        implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
        runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

        // ===== Swagger =====
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

        // ===== QueryDSL =====
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
        annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

        // ===== Lombok =====
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // ===== DB =====
        runtimeOnly 'org.postgresql:postgresql'

        // ===== Test =====
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'org.testcontainers:junit-jupiter:1.19.7'
        testImplementation 'org.testcontainers:postgresql:1.19.7'
        testImplementation 'io.rest-assured:rest-assured'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        // ===== Mac M1 DNS 이슈 =====
        implementation 'io.netty:netty-resolver-dns-native-macos:4.1.104.Final:osx-aarch_64'
    }

    checkstyle {
        maxWarnings = 0
        toolVersion = "10.12.3"
        configFile = file("${rootDir}/naver-checkstyle-rules.xml")
        configProperties = [
                "suppressionFile": "${rootDir}/naver-checkstyle-suppressions.xml"]
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    test {
        useJUnitPlatform {
            excludeTags 'integration'
        }
    }
}
